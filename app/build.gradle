plugins {
    alias(libs.plugins.android.application)
    id 'checkstyle'
    id 'com.google.gms.google-services'
    id 'com.google.dagger.hilt.android'

    alias(libs.plugins.androidx.navigation.safeargs)
}

android {
    namespace 'com.bif.locator'
    compileSdk 36

    defaultConfig {
        def buildNumber = project.hasProperty('BUILD_NUMBER') ? project.getProperty('BUILD_NUMBER').toInteger() : 1

        applicationId "com.bif.locator"
        minSdk 29
        targetSdk 36
        versionCode buildNumber
        versionName "1.0.${buildNumber}"

        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
        multiDexEnabled true
        ndk {
            debugSymbolLevel 'FULL'
        }

        def localProperties = new Properties()
        def localPropertiesFile = rootProject.file('local.properties')
        if (localPropertiesFile.exists()) {
            localProperties.load(new FileInputStream(localPropertiesFile))
        }

        manifestPlaceholders = [
            MAPS_API_KEY: localProperties.getProperty('MAPS_API_KEY', ""),
            PLACES_API_KEY: localProperties.getProperty('PLACES_API_KEY', "")
        ]
    }

    signingConfigs {
        debug {
            storeFile file("debug.keystore")
            storePassword "android"
            keyAlias "androiddebugkey"
            keyPassword "android"
        }
        release {
            storeFile = file("keystore.jks")
            storePassword = System.getenv("SIGNING_STORE_PASSWORD")
            keyAlias = System.getenv("SIGNING_KEY_ALIAS")
            keyPassword = System.getenv("SIGNING_KEY_PASSWORD")
        }
    }

    buildTypes {
        debug {
            signingConfig signingConfigs.debug
        }
        release {
            signingConfig signingConfigs.release
            minifyEnabled true
            shrinkResources true
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
        }
    }
    
    compileOptions {
        sourceCompatibility JavaVersion.VERSION_11
        targetCompatibility JavaVersion.VERSION_11
    }

    testOptions {
        unitTests.returnDefaultValues = true
    }
}

dependencies {
    implementation libs.appcompat
    implementation libs.material
    implementation libs.activity
    implementation libs.constraintlayout
    implementation libs.play.services.maps
    implementation libs.play.services.location
    implementation libs.navigation.fragment.ktx
    implementation libs.navigation.ui.ktx
    implementation libs.google.places
    implementation libs.navigation.runtime
    testImplementation libs.junit
    androidTestImplementation libs.ext.junit
    androidTestImplementation libs.espresso.core
    testImplementation libs.mockito.core
    testImplementation libs.core.testing
    testImplementation libs.mockito.inline

    // Hilt
    implementation libs.hilt.android
    annotationProcessor libs.hilt.compiler

    // For instrumentation tests
    androidTestImplementation libs.hilt.android.testing
    androidTestImplementation libs.core.testing
    androidTestAnnotationProcessor libs.hilt.compiler

    // For local unit tests
    testImplementation libs.hilt.android.testing
    testAnnotationProcessor libs.hilt.compiler
    debugImplementation libs.fragment.testing

    // Import the Firebase BoM
    implementation platform(libs.firebase.bom)

    // TODO: Add the dependencies for Firebase products
    // When using the BoM, don't specify versions in Firebase dependencies
    implementation libs.firebase.analytics

    // Add the dependencies for any other desired Firebase products
    // https://firebase.google.com/docs/android/setup#available-libraries

    // Room
    implementation libs.room.runtime
    annotationProcessor libs.room.compiler

    // Places SDK for searching places
    implementation libs.places
}